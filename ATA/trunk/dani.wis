<WARPIN VERSION="1.0.15" OS="os2_45x">
 <HEAD>
  <TITLE>=("TITLE")</TITLE>
  <REXX NAME=TITLE>
   MyTitle="DaniS506 Driver for OS/2";

   BootDrive = SysBootDrive();
   ConfigSys = BootDrive||'\config.sys';

   /* find AHCI line, if any */
   SearchString='BASEDEV=OS2AHCI.ADD';
   If (SysFileSearch(SearchString,ConfigSys,'test.') \= 0) then test.0 = 0;
   ahci_ln = 0;
   do i = 1 to test.0
     If (TRANSLATE(WORD(test.i, 1)) \= SearchString) then iterate;
     ahci_ln = i;
     leave;
   end

   /* find Dani line, if any */
   SearchString='BASEDEV=DANIS506.ADD';
   If (SysFileSearch(SearchString,ConfigSys,'test.') \= 0) then test.0 = 0;
   dani_ln = 0;
   do i = 1 to test.0
     If (TRANSLATE(WORD(test.i, 1)) \= SearchString) then iterate;
     dani_ln = i;
     leave;
   end

   ConfigSysModify = '';
   if (dani_ln=0) then do
    if (ahci_ln=0) then do
     ConfigSysModify='CONFIGSYS="BASEDEV=DANIS506.ADD /!BIOS | UNIQUE(DANIS506.ADD) ADDTOP"';
    end
    else do
     ConfigSysModify='CONFIGSYS="BASEDEV=DANIS506.ADD /!BIOS | UNIQUE(DANIS506.ADD) ADDAFTER(OS2AHCI.ADD)"';
    end
   end

   rc = WirexxPutEnv('MyTitle',MyTitle);
   rc = WirexxPutEnv('ConfigSysModify',ConfigSysModify);

   Return MyTitle
  </REXX>
  <REXX NAME=GetEnv>
   Parse Arg EnvVar .
   Return WirexxGetEnv(EnvVar)
  </REXX>

  <PCK INDEX=1 PACKAGEID="Arca Noae\DaniS506\Driver\X\X\X"
   TITLE="DaniS506 Driver"
   TARGET="?:\OS2\BOOT" FIXED LONGFILENAMES SELECT
   =("GetEnv ConfigSysModify")
   >The DaniS506 Driver
  </PCK>

  <PCK INDEX=20 PACKAGEID="Arca Noae\DaniS506\Documentation\X\X\X"
   TITLE="DaniS506 Documentation"
   TARGET="?:\ECS\DOC\DaniS506" LONGFILENAMES SELECT
   >ReadMe and License
  </PCK>
</HEAD>
 <BODY>
  <PAGE INDEX=1 TYPE=README>
   <TEXT>Welcome to the =("GetEnv MyTitle")</TEXT>
   <README EXTRACTFROMPCK="20" FORMAT="PLAIN">README</README>
  </PAGE>
  <PAGE INDEX=2 TYPE=CONTAINER>
   <TEXT>Select the packages you want to install.
   </TEXT>
  </PAGE>
  <PAGE INDEX=3 TYPE=CONFIGURE>
    <TEXT>Press "Install" to begin installing this archive.</TEXT>
    <NEXTBUTTON TARGET=0>I~nstall</NEXTBUTTON>
  </PAGE>
 </BODY>
</WARPIN>
